plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
    id "groovy"
}

version "0.1"
group "codes.recursive"
mainClassName = "codes.recursive.Application"
sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

task myRun(type: JavaExec){
    classpath sourceSets.main.runtimeClasspath
    main = mainClassName
    systemProperties = System.properties
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.2.0.BUILD-SNAPSHOT'
    }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly 
}

dependencies {
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'
    compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.4.0-b180830.0438'
    compile 'org.codehaus.groovy:groovy:3.0.0-alpha-4'
    compile "org.codehaus.groovy:groovy-sql:3.0.0-alpha-4"
    compile "org.codehaus.groovy:groovy-json:3.0.0-alpha-4"
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut:micronaut-http-server-netty"
    compile "io.micronaut:micronaut-runtime-groovy"
    compile "io.micronaut:micronaut-validation"

    compile group: 'com.oracle.oci.sdk', name: 'oci-java-sdk-full', version: '1.5.4'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "io.micronaut:micronaut-inject-groovy"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testCompile("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile "io.micronaut.test:micronaut-test-spock:1.0.1"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "io.micronaut.test:micronaut-test-junit5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine"

    developmentOnly "io.micronaut:micronaut-runtime-osx"
}

task copyResources(type:Copy) {
    destinationDir project.buildDir
    from('build-resource') {}
    
    filesMatching([ '**/Dockerfile', '**/app.yaml' ] ) {
        expand(
                [
                        project_artifactId: project.name
                ]
        )
    }

    from('../../misc/atp/wallet_BARNEVENTS/') {
        into 'wallet'
    }

    from('libs') {
        into 'libs/libs'
    }

    from( configurations.runtime ) {
        into 'libs/libs'
    }
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
test.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')

// use JUnit 5 platform
test {
    useJUnitPlatform()
}
tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

jar {
    def libs = configurations.runtime.files.collect { "libs/$it.name" }
    new File('libs/').eachFile { libs << "libs/$it.name" }
    archiveName = "${project.name}.jar"
    manifest {
        attributes ('Main-Class': "${mainClassName}",
                'Class-Path': libs.join(' ')
        )
    }
}

clean {
    // don't delete .oci dir
    delete = [] //clear out existing files to delete
    delete 'build/wallet', 'build/classes', 'build/distributions', 'build/libs', 'build/resources', 'build/scripts', 'build/scriptsShadow', 'build/tmp', 'build/app.yaml', 'build/Dockerfile', 'build/secret.yaml'
}

assemble.dependsOn copyResources